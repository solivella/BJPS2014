model{
  for(i in 1:N){
    y.hat[i] <- alpha[origin[i]] + theta1[origin[i]]*man.change1[i] + theta2[origin[i]]*man.change2[i] + X[i,] %*% betavec
    y[i] ~ dnorm(y.hat[i],tau.y)
    e.y[i] <- y[i] - y.hat[i]
  }
  tau.y <- pow(sigma.y,-2)
  sigma.y ~ dunif(0,100)
  
  for(j in 1:J){
    alpha[j] <- xi.a * B.raw[j,1]
    theta1[j]  <- xi.t1 * B.raw[j,2]
    theta2[j]  <- xi.t2 * B.raw[j,3]
    B.raw[j,1:3] ~ dmnorm (B.raw.hat[j,], Tau.B.raw)
    B.raw.hat[j,1] <- mu.a.raw
    B.raw.hat[j,2] <- mu.t1.raw
    B.raw.hat[j,3] <- mu.t2.raw
  }  
  
  mu.a <- xi.a*mu.a.raw
  mu.t1 <- xi.t1*mu.t1.raw 
  mu.t2 <- xi.t2*mu.t2.raw
  mu.a.raw ~ dnorm (0, .0001) 
  mu.t1.raw ~ dnorm (0, .0001)
  mu.t2.raw ~ dnorm (0, .0001)
  xi.a ~ dunif (0, 100) 
  xi.t1 ~ dunif (0, 100)
  xi.t2 ~ dunif (0, 100)
  
  Tau.B.raw ~ dwish (W, df)
  df <- 4
  Sigma.B.raw <- inverse(Tau.B.raw) 
  sigma.a <- abs(xi.a)*sqrt(Sigma.B.raw[1,1]) 
  sigma.t1 <- abs(xi.t1)*sqrt(Sigma.B.raw[2,2])
  sigma.t2 <- abs(xi.t2)*sqrt(Sigma.B.raw[3,3])
  #rho <- Sigma.B.raw[1,2]/sqrt(Sigma.B.raw[1,1]*Sigma.B.raw[2,2])
  
  for (k in 1:K){
    betavec[k] ~ dnorm (0, .0001)
  }
}